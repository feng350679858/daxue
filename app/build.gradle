apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion System.properties['buildToolsVersion']

    defaultConfig {
        applicationId "com.jingcai.apps.aizhuan"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "2.0"
    }
    signingConfigs {
        release {
            storeFile file('../aizhuan_android.jks')
            keyAlias 'izhuan'
            storePassword 'izhuan365'
            keyPassword 'izhuan365.com'
//			storePassword System.getenv("STORE_PASSWORD")
//			keyPassword System.getenv("KEY_PASSWORD")
//			storePassword System.console().readLine("\n\$ Enter keystore password: ")
//			keyPassword System.console().readLine("\n\$ Enter key password: ")
        }
        debug {
            storeFile file('../debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "Dalegexue_v${defaultConfig.versionName}_${new Date().format('yyyy-MM-dd')}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug{
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }


    productFlavors {
        xiaomi {}
        _360 {}
        baidu {}
        wandoujia {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.jingcai.android.libs:pulltorefresh:1.0.0-SNAPSHOT'
    compile 'com.jingcai.android.libs:toptoast:1.0.0-SNAPSHOT'
    compile 'com.jingcai.android.libs:umenglib:1.0.0-SNAPSHOT'
    compile 'com.jingcai.android.libs:wheel:1.0.0-SNAPSHOT'
//    compile 'com.jingcai.android.libs:qrcode:1.0.0-SNAPSHOT'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.mcxiaoke.volley:library:1.0.15+'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
    compile 'org.bouncycastle:bcprov-ext-jdk15:1.46'
    compile project(':qrcode')
}
